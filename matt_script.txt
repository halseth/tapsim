# Copy taproot, merkle root and key to alt stack
OP_3DUP OP_ROT OP_TOALTSTACK OP_TOALTSTACK OP_TOALTSTACK

# Hash [taproot:merkleroot]
OP_CAT OP_SHA256

# Check input internal key is [x] tweaked with [d = hash(taproot:merkleroot)].
# [d][x]
OP_CHECKINPUTCONTRACT

# On alt stack we now have the committed tap and merkle root.

# Claim [value, preimage]
# Push the hash of the preimage to the alt stack. This will be the new merkle
# leaf.
OP_DUP OP_SHA256 OP_TOALTSTACK 

# Calculate hash of preimage:value
OP_CAT OP_SHA256 

# Use merkle sibling together with new leaf on alt stack to find new merkle
# node.
OP_3DUP OP_DROP OP_FROMALTSTACK
OP_SWAP OP_IF OP_SWAP OP_ENDIF OP_CAT OP_SHA256 OP_TOALTSTACK

# Do the same with the current merkle leaf.
OP_SWAP OP_IF OP_SWAP OP_ENDIF OP_CAT OP_SHA256

# Same for next level in the tree.
OP_3DUP OP_DROP OP_FROMALTSTACK 
OP_SWAP OP_IF OP_SWAP OP_ENDIF OP_CAT OP_SHA256 OP_TOALTSTACK 

OP_SWAP OP_IF OP_SWAP OP_ENDIF OP_CAT OP_SHA256 

# Check merkle root against what we expect. 
OP_FROMALTSTACK OP_FROMALTSTACK OP_SWAP OP_TOALTSTACK
OP_EQUALVERIFY 


# Claim next preimage
OP_DUP OP_SHA256 OP_TOALTSTACK 
# Calculate hash of preimage:value
OP_CAT OP_SHA256 

# Use merkle sibling together with new leaf on alt stack to find new merkle node.
OP_3DUP OP_DROP OP_FROMALTSTACK
OP_SWAP OP_IF OP_SWAP OP_ENDIF OP_CAT OP_SHA256 OP_TOALTSTACK

# Do the same with the current merkle leaf.
OP_SWAP OP_IF OP_SWAP OP_ENDIF OP_CAT OP_SHA256

# Same for next level in the tree.
OP_3DUP OP_DROP OP_FROMALTSTACK 
OP_SWAP OP_IF OP_SWAP OP_ENDIF OP_CAT OP_SHA256 OP_TOALTSTACK 

OP_SWAP OP_IF OP_SWAP OP_ENDIF OP_CAT OP_SHA256 

# Check merkle root against what we expect. 
OP_FROMALTSTACK OP_FROMALTSTACK OP_SWAP OP_TOALTSTACK
OP_EQUALVERIFY 

# New merkle root and existing taproot and key is now on alt stack
# Hash [taproot:merkleroot]
OP_FROMALTSTACK OP_FROMALTSTACK OP_FROMALTSTACK

OP_3DUP OP_DROP OP_CAT OP_SHA256 # -> sha,tap,merkle,x,tap,merkle
OP_ROT OP_SWAP

# Check output key is [x] tweaked with [d = hash(taproot:merkleroot)] and
# [taproot].
# [d][tapoot][x]
OP_CHECKOUTPUTCONTRACT

OP_DROP OP_1


